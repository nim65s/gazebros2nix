final: prev: {
  # keep-sorted start block=yes
  example-robot-data = prev.example-robot-data.overrideAttrs {
    passthru.rosPackage = true;
  };
  zenoh-c = prev.zenoh-c.overrideAttrs (super: {
    # PACKAGE_PREFIX_DIR is $dev
    postInstall = super.postInstall + ''
      substituteInPlace $out/lib/cmake/zenohc/zenohcConfig.cmake \
        --replace-fail "$""{PACKAGE_PREFIX_DIR}" "$out"
    '';
  });
  zenoh-cpp = prev.zenoh-cpp.overrideAttrs (super: {
    patches = (super.patches or [ ]) ++ [
      # fix cmake syntax
      (final.fetchpatch2 {
        url = "https://github.com/eclipse-zenoh/zenoh-cpp/pull/790.patch?full_index=1";
        hash = "sha256-oaCeLTrQ7veWzpTEKGo4pDmNLKmnBIjBkuX71vRtjoo=";
      })
    ];
    postInstall = ""; # already fixed by the patch
  });
  # keep-sorted end

  gazeboPackages = prev.gazeboPackages // {
    fortress = prev.gazeboPackages.fortress.overrideScope (
      _fortress-final: fortress-prev: {
        inherit (final) dartsim urdfdom-headers urdfdom;
        dart = final.dartsim;

        # fast and ugly way to compensate the fact that
        # this version package.xml have no deps info,
        # so we use latest version package.xml,
        # but that is the wrong qt version
        qt6 = final.qt5 // {
          qt5compat = final.qt5.qtquickcontrols2;
        };

        # keep-sorted start block=yes

        ign-common4 = fortress-prev.ign-common4.overrideAttrs (super: {
          propagatedBuildInputs = super.propagatedBuildInputs ++ [ final.freeimage ];
        });
        ign-gui6 = fortress-prev.ign-gui6.overrideAttrs {
          patches = [
            (final.fetchpatch2 {
              url = "https://github.com/gazebosim/gz-gui/pull/696.patch?full_index=1";
              hash = "sha256-MCAkRp7unMx9r2WZnR4vbUpJAeU6Bpku2E7IyAmLoIM=";
            })
          ];
        };
        ign-rendering6 = fortress-prev.ign-rendering6.overrideAttrs (super: {
          propagatedBuildInputs = super.propagatedBuildInputs ++ [ final.freeimage ];
        });
        ign-tools1 = fortress-prev.ign-tools1.overrideAttrs (super: {
          # ref. https://github.com/gazebosim/gz-tools/pull/173 merged
          postPatch = ''
            substituteInPlace CMakeLists.txt --replace-fail \
              "cmake_minimum_required(VERSION 2.8.12 FATAL_ERROR)" \
              "cmake_minimum_required(VERSION 3.10 FATAL_ERROR)"
          '';
          dontWrapQtApps = false;
          nativeBuildInputs = super.nativeBuildInputs ++ [ final.qt5.wrapQtAppsHook ];
          qtWrapperArgs = [
            "--set-default"
            "QT_QPA_PLATFORM"
            "xcb"
          ];
          postFixup = "wrapQtApp $out/bin/ign";
        });
        ign-transport11 = fortress-prev.ign-transport11.overrideAttrs {
          postFixup = ''
            substituteInPlace $out/lib/ruby/gz/cmdtransport11.rb --replace-fail \
              '"../../..//nix/store' \
              '"/nix/store'
          '';
        };
        # keep-sorted end
      }
    );

    harmonic = prev.gazeboPackages.harmonic.overrideScope (
      _harmonic-final: harmonic-prev: {
        inherit (final) dartsim urdfdom-headers urdfdom;
        dart = final.dartsim;

        # keep-sorted start block=yes

        gz-common5 = harmonic-prev.gz-common5.overrideAttrs {
          patches = [
            (final.fetchpatch {
              url = "https://github.com/nim65s/gz-common/commit/d21c3dfce2bbe463f888ed0ede37c6d483b8a49f.patch?full_index=1";
              hash = "sha256-uWNzRcbEg8b7ApJ3jKQqMQSUSGFAyJ9U18dCPzDwJhI=";
            })
          ];
        };
        gz-gui8 = harmonic-prev.gz-gui8.overrideAttrs {
          patches = [
            (final.fetchpatch2 {
              url = "https://github.com/gazebosim/gz-gui/pull/677.patch?full_index=1";
              hash = "sha256-JQgHaNAOv36K4c+i+Fn5+ZRb0LK9E7UExm2SKBYKKFo=";
            })
            (final.fetchpatch2 {
              url = "https://github.com/gazebosim/gz-gui/pull/745.patch?full_index=1";
              hash = "sha256-vlgLYgSHZC/ga2xxgI6AJapq45K3xH9IQBSCOct5Yx0=";
            })
          ];
        };
        gz-launch7 = harmonic-prev.gz-launch7.overrideAttrs {
          # ref. https://github.com/gazebosim/gz-launch/pull/329
          postPatch = ''
            substituteInPlace plugins/websocket_server/WebsocketServer.cc \
              --replace-fail '((_op)+","+(_topic)+","+(_type)+",")' '((_op)+","+(_topic)+","+(std::string(_type))+",")'
          '';
        };
        gz-msgs10 = harmonic-prev.gz-msgs10.overrideAttrs {
          patches = [
            (final.fetchpatch2 {
              url = "https://github.com/gazebosim/gz-msgs/pull/501.patch?full_index=1";
              hash = "sha256-0uscwyYZafHfzooxcrrhtcfcxknpDTEcZ6Ie0WWySVw=";
            })
          ];
        };
        gz-plugin2 = harmonic-prev.gz-plugin2.overrideAttrs {
          postFixup = ''
            substituteInPlace $out/lib/ruby/gz/cmdplugin2.rb --replace-fail \
              '"../../..//nix/store' \
              '"/nix/store'
          '';
        };
        gz-rendering8 = harmonic-prev.gz-rendering8.overrideAttrs {
          patches = [
            (final.fetchpatch {
              url = "https://github.com/gazebosim/gz-rendering/commit/80b6a05e535cc818d6f8590bc86f6a823f35e471.patch?full_index=1";
              hash = "sha256-ekKz8p2YBLLakVijhGS6+e6x98Zl8kx4Hg3PRJDkM5M=";
            })
          ];
        };
        gz-transport13 = harmonic-prev.gz-transport13.overrideAttrs {
          postFixup = ''
            substituteInPlace $out/lib/ruby/gz/cmdtransport13.rb --replace-fail \
              '"../../..//nix/store' \
              '"/nix/store'
          '';
        };
        # keep-sorted end
      }
    );

    ionic = prev.gazeboPackages.ionic.overrideScope (
      _ionic-final: ionic-prev: {
        inherit (final) dartsim urdfdom-headers urdfdom;
        dart = final.dartsim;

        # keep-sorted start block=yes

        gz-launch8 = ionic-prev.gz-launch8.overrideAttrs {
          # ref. https://github.com/gazebosim/gz-launch/pull/330
          postPatch = ''
            substituteInPlace plugins/websocket_server/WebsocketServer.cc \
              --replace-fail '((_op)+","+(_topic)+","+(_type)+",")' '((_op)+","+(_topic)+","+(std::string(_type))+",")'
          '';
        };
        gz-msgs11 = ionic-prev.gz-msgs11.overrideAttrs {
          postFixup = ''
            substituteInPlace $out/lib/ruby/gz/cmdmsgs11.rb --replace-fail \
              "'../../..//nix/store" \
              "'/nix/store"
          '';
        };
        gz-plugin3 = ionic-prev.gz-plugin3.overrideAttrs {
          postFixup = ''
            substituteInPlace $out/lib/ruby/gz/cmdplugin3.rb --replace-fail \
              '"../../..//nix/store' \
              '"/nix/store'
          '';
        };
        gz-sdformat15 = ionic-prev.gz-sdformat15.overrideAttrs {
          postFixup = ''
            substituteInPlace $out/lib/ruby/gz/cmdsdformat15.rb --replace-fail \
              '"../../..//nix/store' \
              '"/nix/store'
          '';
        };
        gz-sim9 = ionic-prev.gz-sim9.overrideAttrs {
          postFixup = ''
            substituteInPlace $out/lib/ruby/gz/cmdmodel9.rb --replace-fail \
              '"../../..//nix/store' \
              '"/nix/store'
          '';
        };
        # keep-sorted end
      }
    );

    jetty = prev.gazeboPackages.jetty.overrideScope (
      _jetty-final: jetty-prev: {
        inherit (final) dartsim urdfdom-headers urdfdom;
        dart = final.dartsim;

        # keep-sorted start block=yes

        gz-msgs12 = jetty-prev.gz-msgs12.overrideAttrs {
          postFixup = ''
            substituteInPlace $out/lib/ruby/gz/cmdmsgs12.rb --replace-fail \
              "'../../..//nix/store" \
              "'/nix/store"
          '';
        };
        gz-plugin4 = jetty-prev.gz-plugin4.overrideAttrs {
          postFixup = ''
            substituteInPlace $out/lib/ruby/gz/cmdplugin4.rb --replace-fail \
              '"../../..//nix/store' \
              '"/nix/store'
          '';
        };
        gz-sdformat16 = jetty-prev.gz-sdformat16.overrideAttrs {
          postFixup = ''
            substituteInPlace $out/lib/ruby/gz/cmdsdformat16.rb --replace-fail \
              '"../../..//nix/store' \
              '"/nix/store'
          '';
        };
        gz-sim10 = jetty-prev.gz-sim10.overrideAttrs (super: {
          postPatch = (super.postPatch or "") + ''
            substituteInPlace src/cmd/CMakeLists.txt \
              --replace-fail "$<TARGET_FILE_NAME:$""{model_executable}>" "$""{model_executable}" \
              --replace-fail "$<TARGET_FILE_NAME:$""{sim_executable}>" "$""{sim_executable}" \
              --replace-fail "$<TARGET_FILE_NAME:$""{gui_executable}>" "$""{gui_executable}" \
              --replace-fail "$""{CMAKE_INSTALL_LIBEXECDIR}" "libexec"
          '';
        });
        gz-transport15 = jetty-prev.gz-transport15.overrideAttrs {
          postFixup = ''
            substituteInPlace $out/lib/ruby/gz/cmd{transport,log}15.rb --replace-fail \
              '../../..//nix/store' \
              '/nix/store'
          '';
        };
        # keep-sorted end
      }
    );
  };

  rosPackages =
    let
      # some packages need an installed verison of themself discoverable by ament.
      # maybe we could use build dir.
      amentInstallCheckOverride = {
        checkTarget = " ";
        doInstallCheck = true;
        preInstallCheck = "export AMENT_PREFIX_PATH=$out:$AMENT_PREFIX_PATH";
        installCheckTarget = "test";
      };

      rosOverlay = ros-final: ros-prev: {
        inherit (final)
          dartsim
          fcl
          urdfdom-headers
          urdfdom
          octomap
          ;
        inherit (final.python3Packages)
          colmpc
          mim-solvers
          ;
        # keep-sorted start block=yes
        agimus-controller-ros = ros-prev.agimus-controller-ros.overrideAttrs {
          # this thing believe we did pass --build-directory or --build-base:
          # https://github.com/PickNikRobotics/generate_parameter_library/blob/main/generate_parameter_library_py/generate_parameter_library_py/setup_helper.py
          postPatch = ''
            substituteInPlace setup.py \
              --replace-fail \
                "from generate_parameter_library_py.setup_helper import generate_parameter_module" \
                "from generate_parameter_library_py.generate_python_module import run" \
              --replace-fail \
                "generate_parameter_module(module_name, yaml_file)" \
                "run(f\"$out/${ros-final.python3.sitePackages}/agimus_controller_ros/{module_name}.py\", yaml_file)"
          '';
        };
        agimus-demos = ros-prev.agimus-demos.overrideAttrs (super: {
          nativeBuildInputs = (super.nativeBuildInputs or [ ]) ++ [
            ros-final.ament-cmake
            final.cmake
            final.python3
          ];
        });
        agimus-franka-description = ros-prev.agimus-franka-description.overrideAttrs amentInstallCheckOverride;
        agimus-franka-example-controllers = ros-prev.agimus-franka-example-controllers.overrideAttrs amentInstallCheckOverride;
        agimus-franka-fr3-moveit-config = ros-prev.agimus-franka-fr3-moveit-config.overrideAttrs amentInstallCheckOverride;
        agimus-franka-msgs = ros-prev.agimus-franka-msgs.overrideAttrs {
          cmakeFlags = [
            "-DCMAKE_SKIP_BUILD_RPATH=ON"
            "-DCMAKE_BUILD_WITH_INSTALL_RPATH=ON"
          ];
        };
        agimus-franka-robot-state-broadcaster =
          ros-prev.agimus-franka-robot-state-broadcaster.overrideAttrs
            (super: {
              nativeBuildInputs = super.nativeBuildInputs ++ [ final.ctestCheckHook ];
              disabledTests = [ "test_load_agimus_franka_robot_state_broadcaster" ]; # TODO: ???
            });
        agimus-franka-semantic-components = ros-prev.agimus-franka-semantic-components.overrideAttrs amentInstallCheckOverride;
        agimus-msgs = ros-prev.agimus-msgs.overrideAttrs {
          cmakeFlags = [
            "-DCMAKE_SKIP_BUILD_RPATH=ON"
            "-DCMAKE_BUILD_WITH_INSTALL_RPATH=ON"
          ];
        };
        gz-dartsim = final.dartsim;
        linear-feedback-controller = ros-prev.linear-feedback-controller.overrideAttrs {
          doCheck = true;
          preCheck = ''
            export LD_LIBRARY_PATH=.
          '';
        };
        linear-feedback-controller-msgs = ros-prev.linear-feedback-controller-msgs.overrideAttrs {
          doCheck = true;
          cmakeFlags = [
            "-DCMAKE_SKIP_BUILD_RPATH=ON"
            "-DCMAKE_BUILD_WITH_INSTALL_RPATH=ON"
          ];
        };
        # keep-sorted end
      };
      gzVendorOverlay =
        ros-final: ros-prev:
        final.lib.genAttrs'
          [
            "gz-cmake"
            "gz-common"
            "gz-dartsim"
            "gz-fuel-tools"
            "gz-gui"
            "gz-launch"
            "gz-math"
            "gz-msgs"
            "gz-physics"
            "gz-plugin"
            "gz-rendering"
            "gz-sensors"
            "gz-sim"
            "gz-tools"
            "gz-transport"
            "gz-utils"
            "sdformat"
          ]
          (
            pkg:
            final.lib.nameValuePair "${pkg}-vendor" (
              ros-prev."${pkg}-vendor".overrideAttrs (
                {
                  # cmakeFlags ? [ ],
                  propagatedBuildInputs ? [ ],
                  ...
                }:
                {
                  # cmakeFlags = cmakeFlags ++ [ (final.lib.cmakeFeature "AMENT_VENDOR_POLICY" "NEVER_VENDOR") ];
                  cmakeFlags = [ (final.lib.cmakeFeature "AMENT_VENDOR_POLICY" "NEVER_VENDOR") ];
                  propagatedBuildInputs = propagatedBuildInputs ++ [ ros-final."${pkg}" ];
                }
              )
            )
          );
    in
    prev.rosPackages
    // {
      humble = prev.rosPackages.humble.overrideScope (
        humble-final: humble-prev:
        (rosOverlay humble-final humble-prev)
        // {
          gazebo_11 = null;
          gazebo-planar-move-plugin = null;
          gazebo-ros = null;
          gazebo-dev = null;

          # keep-sorted start block=yes

          _unresolved_ignition-gazebo6 = humble-final.ignition-gazebo6;
          _unresolved_ignition-plugin = humble-final.ignition-plugin;
          agimus-franka-ign-ros2-control = humble-prev.agimus-franka-ign-ros2-control.overrideAttrs {
            env.ROS_DISTRO = "humble";
            env.IGNITION_VERSION = "fortress";
            env.IGN_VERSION = "fortress";
          };
          launch-testing = humble-prev.launch-testing.overrideAttrs (super: {
            patches = (super.patches or [ ]) ++ [
              (final.fetchpatch2 {
                url = "https://github.com/ros2/launch/pull/991.patch?full_index=1";
                stripLen = 1;
                hash = "sha256-OOa03NANO9nNgDqNRcvUwWRa7jWCxZreYRosvHP6818=";
              })
            ];
          });
          # that repo somehow has a 0.0.0 tag
          net-ft-description = humble-prev.net-ft-description.overrideAttrs (super: {
            src = final.fetchFromGitHub {
              inherit (super.src) owner repo;
              rev = "f76040b53ce1bc021cba89fdca35089b8e883a16";
              hash = "sha256-A+A9c1N/2QShCk9z65PbBT4KvM4C+85X1Suai5bGGWM=";
            };
          });
          net-ft-diagnostic-broadcaster = humble-prev.net-ft-diagnostic-broadcaster.overrideAttrs {
            src = humble-final.net-ft-description.src;
          };
          net-ft-driver = humble-prev.net-ft-driver.overrideAttrs {
            src = humble-final.net-ft-description.src;
          };
          play-motion2 = humble-prev.play-motion2.overrideAttrs (super: {
            # ref. https://github.com/ros2/rclcpp/pull/3211
            postPatch = (super.postPatch or "") + ''
              sed -i "1i #include <functional>" src/utils/motion_loader.*
            '';
          });
          python-with-ament-package =
            let
              # TODO: this make no sense
              python = humble-final.python3.withPackages (p: [
                humble-final.ament-package
                p.catkin-pkg
              ]);
            in
            "${python}/${python.sitePackages}";
          ros-gz = humble-prev.ros-gz.overrideAttrs (_super: {
            env.PYTHONPATH = humble-final.python-with-ament-package;
            meta.platforms = final.lib.platforms.linux;
          });
          ros-gz-sim = humble-prev.ros-gz-sim.overrideAttrs (super: {
            propagatedNativeBuildInputs = (super.propagatedNativeBuildInputs or [ ]) ++ [
              humble-final.gz-tools
            ];
          });
          ros-gz-sim-demos = null; # wants qt-gui-cpp, where qt5 and python 3.13 are not compatible
          sdformat-urdf = humble-prev.sdformat-urdf.overrideAttrs {
            # ref. https://github.com/ros/sdformat_urdf/pull/41 merged
            postPatch = ''
              substituteInPlace CMakeLists.txt --replace-fail \
                "find_package(urdfdom_headers 1.0.6 REQUIRED)" \
                "find_package(urdfdom_headers REQUIRED)"
            '';
          };
          topic-tools-interfaces = humble-prev.topic-tools-interfaces.overrideAttrs {
            doCheck = false;
          };
          # keep-sorted end
        }
      );

      jazzy =
        (prev.rosPackages.jazzy.overrideScope (
          jazzy-final: jazzy-prev:
          (rosOverlay jazzy-final jazzy-prev) // (gzVendorOverlay jazzy-final jazzy-prev)
        )).overrideScope
          (
            jazzy-final: jazzy-prev: {
              # keep-sorted start block=yes
              agimus-franka-hardware = jazzy-prev.agimus-franka-hardware.overrideAttrs {
                doCheck = false; # TODO
              };
              br2-gazebo-worlds = jazzy-prev.br2-gazebo-worlds.overrideAttrs {
                patches = [
                  # ref. https://github.com/Tiago-Pro-Harmonic/br2_gazebo_worlds/pull/1
                  (final.fetchpatch {
                    url = "https://github.com/nim65s/br2_gazebo_worlds/commit/8a2bf334bc3b286ed4187fe9ffcd723113794d0b.patch?full_index=1";
                    hash = "sha256-1j2RgxBOXYitRXeVJt3MJQXGq6H70GgvBB6Cu40/63M=";
                  })
                ];
              };
              gazebo-planar-move-plugin = null;
              gz-dartsim-vendor = jazzy-prev.gz-dartsim-vendor.overrideAttrs {
                # env.GZ_RELAX_VERSION_MATCH = ""; TODO
                postPatch = ''
                  substituteInPlace CMakeLists.txt --replace-fail \
                    "$""{VERSION_MATCH} $""{LIB_VER_MAJOR}.$""{LIB_VER_MINOR}" ""
                '';
              };
              gz-gui-vendor = jazzy-prev.gz-gui-vendor.overrideAttrs {
                postInstall = "";
              };
              gz-tools-vendor = jazzy-prev.gz-tools-vendor.overrideAttrs {
                postFixup = "";
                qtWrapperArgs = [ ];
              };
              launch-testing = jazzy-prev.launch-testing.overrideAttrs (super: {
                patches = (super.patches or [ ]) ++ [
                  (final.fetchpatch2 {
                    url = "https://github.com/ros2/launch/pull/972.patch?full_index=1";
                    stripLen = 1;
                    includes = [ "launch_testing/*" ];
                    hash = "sha256-p7RoxvSUBsbnoxweS5KbdrlF9eGnxohy8VAGpMAQchc=";
                  })
                ];
              });
              launch-testing-ros = jazzy-prev.launch-testing-ros.overrideAttrs (super: {
                patches = (super.patches or [ ]) ++ [
                  (final.fetchpatch2 {
                    url = "https://github.com/ros2/launch_ros/pull/540.patch?full_index=1";
                    stripLen = 1;
                    hash = "sha256-lv8R9lij5gwTvShmpLD8bkTu/WcIAdGWAV7qEz0UmF8=";
                  })
                ];
              });
              moveit-task-constructor-core = jazzy-prev.moveit-task-constructor-core.overrideAttrs (super: {
                # TODO: unvendor pybind11 upstream
                cmakeFlags = (super.cmakeFlags or [ ]) ++ [ "-DPYBIND11_INSTALL=OFF" ];
                postFixup = ''
                  rm \
                    $out/${jazzy-final.python3.sitePackages}/moveit/__init__.py \
                    $out/${jazzy-final.python3.sitePackages}/moveit/__pycache__/__init__.cpython-*.pyc
                '';
              });
              net-ft-description = jazzy-prev.net-ft-description.overrideAttrs (super: {
                # ref. https://github.com/gbartyzel/ros2_net_ft_driver/pull/25 merged
                src = final.fetchFromGitHub {
                  inherit (super.src) owner repo;
                  rev = "a2770efe5d4ec3560fd35b4672a3b59d15c37d30";
                  hash = "sha256-9kjfo4We1OQLgi5g9cMz3ync1vp4HiJPbE1NnQqA96A=";
                };
              });
              net-ft-diagnostic-broadcaster = jazzy-prev.net-ft-diagnostic-broadcaster.overrideAttrs {
                src = jazzy-final.net-ft-description.src;
              };
              net-ft-driver = jazzy-prev.net-ft-driver.overrideAttrs {
                src = jazzy-final.net-ft-description.src;
              };
              odri-dual-motor-testbed-bringup = jazzy-prev.odri-dual-motor-testbed-bringup.overrideAttrs {
                doCheck = false; # TODO: cppcheck + cpplint + uncrustify
              };
              odri-dual-motor-testbed-description = jazzy-prev.odri-dual-motor-testbed-description.overrideAttrs {
                doCheck = false; # TODO: cppcheck + cpplint + uncrustify
              };
              pal-gazebo-plugins = null;
              pal-gazebo-worlds = null;
              pal-maps = null;
              # TODO: does not seem useful for now, but might bite later
              realsense-gazebo-plugin = null;
              # ros-gz-bridge = jazzy-prev.ros-gz-bridge.overrideAttrs {
              #   cmakeFlags = [
              #     "-DGZ_MSGS_VERSION_FULL=${jazzy-final.gz-msgs.version}"
              #   ];
              # };
              # ros-gz-sim = jazzy-prev.ros-gz-sim.overrideAttrs (super: {
              #   cmakeFlags = [ "-DGZ_SIM_VER=${final.lib.versions.major jazzy-final.gz-sim.version}" ];
              #   propagatedNativeBuildInputs = (super.propagatedNativeBuildInputs or [ ]) ++ [
              #     jazzy-final.gz-tools
              #   ];
              # });
              sdformat-urdf = jazzy-prev.sdformat-urdf.overrideAttrs {
                postPatch = ''
                  # ref. https://github.com/ros/sdformat_urdf/pull/42
                  substituteInPlace CMakeLists.txt --replace-fail \
                    "find_package(urdfdom_headers 1.0.6 REQUIRED)" \
                    "find_package(urdfdom_headers REQUIRED)"
                '';
              };
              tiago-pro-2dnav = null;
              tiago-pro-laser-sensors = null;
              tiago-pro-rgbd-sensors = null;
              # keep-sorted end
            }
          );

      kilted = prev.rosPackages.kilted.overrideScope (
        kilted-final: kilted-prev:
        (rosOverlay kilted-final kilted-prev)
        // (gzVendorOverlay kilted-final kilted-prev)
        // {
          launch-testing = kilted-prev.launch-testing.overrideAttrs (super: {
            patches = (super.patches or [ ]) ++ [
              (final.fetchpatch2 {
                url = "https://github.com/ros2/launch/pull/972.patch?full_index=1";
                stripLen = 1;
                includes = [ "launch_testing/*" ];
                hash = "sha256-p7RoxvSUBsbnoxweS5KbdrlF9eGnxohy8VAGpMAQchc=";
              })
            ];
          });
          launch-testing-ros = kilted-prev.launch-testing-ros.overrideAttrs (super: {
            patches = (super.patches or [ ]) ++ [
              (final.fetchpatch2 {
                url = "https://github.com/ros2/launch_ros/pull/540.patch?full_index=1";
                stripLen = 1;
                hash = "sha256-lv8R9lij5gwTvShmpLD8bkTu/WcIAdGWAV7qEz0UmF8=";
              })
            ];
          });
          sdformat-urdf = kilted-prev.sdformat-urdf.overrideAttrs {
            postPatch = ''
              substituteInPlace CMakeLists.txt --replace-fail \
                "find_package(urdfdom_headers 1.0.6 REQUIRED)" \
                "find_package(urdfdom_headers REQUIRED)"
            '';
          };
        }
      );

      rolling = prev.rosPackages.rolling.overrideScope (
        rolling-final: rolling-prev:
        (rosOverlay rolling-final rolling-prev)
        // (gzVendorOverlay rolling-final rolling-prev)
        // {
          parameter-traits = null;
        }
      );
    };
}
